apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'application'

sourceCompatibility = 1.8
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

mainClassName = "com.wehavescience.jetty.ApplicationRunner"

// Fills out all dependencies which are necessary to start the embedded jetty into our war file
configurations {
    embeddedJetty
}
dependencies {
    embeddedJetty 'org.eclipse.jetty:jetty-server:9.2.2.v20140723'
    embeddedJetty 'org.eclipse.jetty:jetty-webapp:9.2.2.v20140723'
}

war.baseName = rootProject.name
war {
    // unzip and add all jetty dependencies into the root of our war file
    from {configurations.embeddedJetty.collect {
        project.zipTree(it)
    }
    }
    // remove signature and unnecessary files
    exclude "META-INF/*.SF", "META-INF/*.RSA", "about.html", "about_files/**", "readme.txt", "plugin.properties", "jetty-dir.css"

    // include in the root of the war only the classes which will be used to start the embedded Jetty
    from 'build/classes/main'

    webInf { from 'src/main/webapp/WEB-INF' } // adds a file-set to the WEB-INF dir.

    // tells the class to run when the generate war be executed using 'java -jar'
    manifest { attributes 'Main-Class': 'com.wehavescience.app.Starter' }
}

dependencies {
    compile 'org.projectlombok:lombok:1.14.4'

    compile 'org.jboss.weld.servlet:weld-servlet:2.2.4.Final'
    compile 'org.jboss.weld:weld-core:2.2.4.Final'

    compile 'javax.servlet:javax.servlet-api:3.1.0'

    compile 'org.mongodb.morphia:morphia:0.108'

    compile 'org.glassfish.jersey.core:jersey-server:2.11'
    compile 'org.glassfish.jersey.containers:jersey-container-servlet:2.11'
    compile 'org.glassfish.jersey.containers.glassfish:jersey-gf-cdi:2.11'
    compile 'com.google.code.gson:gson:2.2.4'

    compile 'org.hibernate:hibernate-validator:5.1.2.Final'
    compile 'org.jboss:jandex:1.2.1.Final'
    compile 'com.mchange:c3p0:0.9.2.1'
    compile 'org.hibernate:hibernate-c3p0:4.3.6.Final'
    compile 'mysql:mysql-connector-java:5.1.31'

    compile 'org.eclipse.jetty:jetty-server:9.2.2.v20140723'
    compile 'org.eclipse.jetty:jetty-webapp:9.2.2.v20140723'
    compile 'org.eclipse.jetty:jetty-jsp:9.2.2.v20140723'
    compile 'org.eclipse.jetty:jetty-plus:9.2.2.v20140723'

    compile 'com.github.spullara.mustache.java:compiler:0.8.16'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}